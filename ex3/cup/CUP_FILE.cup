/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import AST.*;

parser code 
{:
	public Lexer lexer;

	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
	}
	public void report_error(String message, Object info)
	{
		System.out.print("ERROR >> ");		
		System.out.print("[");
		System.out.print(lexer.getLine());
		System.out.print(":");
		System.out.print(lexer.getCharPos());
		System.out.print("] ");
		throw new RuntimeException(Integer.toString(lexer.getLine()));
		// System.exit(0);
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
	s = lexer.next_token();
	//System.out.print(s.sym);
	if(s.sym == TokenNames.TOKEN_ERROR) {
	        throw new RuntimeException("lexical");
	    }
	System.out.print("[");
	System.out.print(lexer.getLine());
	System.out.print(":");
	System.out.print(lexer.getCharPos());
	System.out.print("] ");
	System.out.print(TokenNames.terminalNames[s.sym]);
	if (s.value != null)
	{
		System.out.print("( ");
		System.out.print(s.value);
		System.out.print(" )");
	}
	System.out.print("\n");
	return s; 
:};

/*************/
/* TERMINALS */
/*************/
terminal IF;
terminal EQ;
terminal DOT;
terminal COMMA;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal WHILE;
terminal DIVIDE;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal ASSIGN;
terminal ARRAY;
terminal CLASS;
terminal EXTENDS;
terminal RETURN;
terminal NEW;
terminal TYPE_INT;
terminal TYPE_STRING;
terminal COMMENT;
terminal TOKEN_ERROR;
terminal TYPE_VOID;
terminal LT;
terminal GT;
terminal NIL;

/*************/
/* TERMINALS */
/*************/
terminal Integer INT;
terminal String ID;
terminal String STRING;

/*****************/
/* NON TERMINALS */
/*****************/
non terminal AST_PROGRAM Program;
non terminal AST_DEC dec;
non terminal AST_TYPE type;
non terminal AST_DEC arrayTypedef;
non terminal AST_DEC varDec;
non terminal AST_FUNCARGS funcArgs;
non terminal AST_DEC funcDec;
non terminal AST_CFIELD cField;
non terminal AST_CFIELD_LIST cFieldList;
non terminal AST_DEC classDec;
non terminal AST_VAR var;
non terminal AST_STMT stmt;
non terminal AST_STMT_LIST stmtList;
non terminal AST_EXP exp;
non terminal AST_EXPLIST expList;
non terminal AST_NEW_EXP newExp;
non terminal AST_EXP_BINOP binopExp;


/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence nonassoc ASSIGN;
precedence left EQ;
precedence left GT, LT;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence nonassoc LBRACK;
precedence nonassoc LPAREN;
precedence left DOT;

/************************/
/* START WITH: Program */
/************************/
start with Program;

/********************/
/* DERIVATION RULES */
/********************/

Program     ::=     dec:d Program:p                   {: RESULT = new AST_PROGRAM(d, p); :}
                    | dec:d                           {: RESULT = new AST_PROGRAM(d, null); :}
                    ;

dec         ::=     varDec:v                            {: RESULT = v; :}
                    | funcDec:v                        {: RESULT = v; :}
                    | classDec:v                       {: RESULT = v; :}
                    | arrayTypedef:v                {: RESULT = v; :}
                    ;

type        ::=     TYPE_INT                         {: RESULT = new AST_TYPE_INT(); :}
                    | TYPE_STRING                    {: RESULT = new AST_TYPE_STRING(); :}
                    | TYPE_VOID                      {: RESULT = new AST_TYPE_VOID(); :}
                    | ID:name                        {: RESULT = new AST_TYPE_SIMPLE(name); :}
                    ;

arrayTypedef::=     ARRAY ID:name EQ type:t LBRACK RBRACK SEMICOLON    {: RESULT = new AST_ARRAY_TYPE_DEF(name, t); :}
                    ;

varDec      ::=     type:t ID:name ASSIGN exp:e SEMICOLON               {: RESULT = new AST_VARDEC_ASSIGN(t, name, e); :}
                    | type:t ID:name SEMICOLON                          {: RESULT = new AST_VARDEC_REG(t, name); :}
                    | type:t ID:name ASSIGN newExp:ne SEMICOLON         {: RESULT = new AST_VARDEC_NEW(t, name, ne); :}
                    ;

funcArgs     ::=     type:t ID:n COMMA funcArgs:fa                       {: RESULT = new AST_FUNCARGS(t, n, fa); :}
                    | type:t ID:n                                        {: RESULT = new AST_FUNCARGS(t, n, null); :}
                    ;

funcDec     ::=     type:t ID:n1 LPAREN funcArgs:fa RPAREN LBRACE stmtList:sl RBRACE {: RESULT = new AST_FUNCDEC(t, n1, fa, sl); :}
                    | type:t ID:n1 LPAREN RPAREN LBRACE stmtList:sl RBRACE           {: RESULT = new AST_FUNCDEC(t, n1, null, sl); :}
                    ;

cField      ::=     varDec:v                            {: RESULT = new AST_CFIELD_VAR(v); :}
                    | funcDec:v                         {: RESULT = new AST_CFIELD_FUNC(v); :}
                    ;

cFieldList  ::=     cField:cf cFieldList:fl                               {: RESULT = new AST_CFIELD_LIST(cf, fl); :}
                    | cField:cf                                           {: RESULT = new AST_CFIELD_LIST(cf, null); :}
                    ;

classDec    ::=     CLASS ID:name LBRACE cFieldList:fl RBRACE                       {: RESULT = new AST_CLASS_DEC(name, null, fl); :}
                    | CLASS ID:name EXTENDS ID:name2 LBRACE cFieldList:fl RBRACE    {: RESULT = new AST_CLASS_DEC(name, name2, fl); :}
                    ;


var			::=		ID:name								{: RESULT = new AST_VAR_SIMPLE(name);       :}
					| var:v DOT ID:fieldName			{: RESULT = new AST_VAR_FIELD(v,fieldName); :}
					| var:v LBRACK exp:e RBRACK			{: RESULT = new AST_VAR_SUBSCRIPT(v,e);     :}
					;

stmt		::=		var:v ASSIGN exp:e SEMICOLON								{: RESULT = new AST_STMT_ASSIGN(v, e); :}
                    | varDec:vd								                    {: RESULT = new AST_STMT_VARDEC(vd); :}
                    | var:v ASSIGN newExp:ne SEMICOLON                          {: RESULT = new AST_STMT_ASSIGN_NEW(v, ne); :}
                    | RETURN exp:e SEMICOLON                                    {: RESULT = new AST_STMT_RETURN(e); :}
                    | RETURN SEMICOLON                                          {: RESULT = new AST_STMT_RETURN(null); :}
					| IF    LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_IF(cond,body); :}
					| WHILE LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_WHILE(cond,body); :}
					| ID:name LPAREN expList:e RPAREN SEMICOLON                 {: RESULT = new AST_STMT_EXPLIST(null, name, e); :}
					| var:v DOT ID:name LPAREN expList:e RPAREN SEMICOLON       {: RESULT = new AST_STMT_EXPLIST(v, name, e); :}
					| ID:name LPAREN RPAREN SEMICOLON                           {: RESULT = new AST_STMT_EXPLIST(null, name, null); :}
					| var:v DOT ID:name LPAREN RPAREN SEMICOLON                 {: RESULT = new AST_STMT_EXPLIST(v, name, null); :}
					;

stmtList	::=		stmt:s	stmtList:l		            {: RESULT = new AST_STMT_LIST(s,l);    :}
					| stmt:s				            {: RESULT = new AST_STMT_LIST(s,null); :}
					;

exp			::=		var:v								        {: RESULT = new AST_EXP_VAR(v); :}
					| LPAREN exp:e RPAREN                       {: RESULT = new AST_EXP_PAREN(e); :}
					| ID:name LPAREN expList:el RPAREN             {: RESULT = new AST_EXP_FCALL(null, name, el); :}
					| ID:name LPAREN RPAREN             {: RESULT = new AST_EXP_FCALL(null, name, null); :}
					| var:v DOT ID:name LPAREN expList:el RPAREN   {: RESULT = new AST_EXP_FCALL(v, name, el); :}
					| var:v DOT ID:name LPAREN RPAREN           {: RESULT = new AST_EXP_FCALL(v, name, null); :}
					| NIL:n                                     {: RESULT = new AST_EXP_NIL(); :}
					| STRING:s                                  {: RESULT = new AST_EXP_STRING(s); :}
					| INT:i								        {: RESULT = new AST_EXP_INT(i);    :}
					| MINUS INT:i                               {: RESULT = new AST_EXP_INT(-i);   :}
					| binopExp:e                                {: RESULT = e; :}
					;

expList     ::=     exp:e COMMA expList:el                  {: RESULT = new AST_EXPLIST(e, el); :}
                    | exp:e                                 {: RESULT = new AST_EXPLIST(e, null); :}
                    ;

newExp      ::=     NEW type:name                         {: RESULT = new AST_NEW_EXP(name, null); :}
                    | NEW type:name LBRACK exp:e RBRACK   {: RESULT = new AST_NEW_EXP(name, e); :}
                    ;


binopExp    ::=     exp:e1 PLUS exp:e2                            {: RESULT = new AST_EXP_BINOP(e1, e2, 0); :}
                    | exp:e1 MINUS exp:e2                         {: RESULT = new AST_EXP_BINOP(e1, e2, 1); :}
                    | exp:e1 TIMES exp:e2                         {: RESULT = new AST_EXP_BINOP(e1, e2, 2); :}
                    | exp:e1 DIVIDE exp:e2                        {: RESULT = new AST_EXP_BINOP(e1, e2, 3); :}
                    | exp:e1 LT exp:e2                            {: RESULT = new AST_EXP_BINOP(e1, e2, 4); :}
                    | exp:e1 GT exp:e2                            {: RESULT = new AST_EXP_BINOP(e1, e2, 5); :}
                    | exp:e1 EQ exp:e2                            {: RESULT = new AST_EXP_BINOP(e1, e2, 6); :}
                    ;


